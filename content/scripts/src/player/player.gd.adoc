---
title: res://src/player/player.gd
---
:toc:

== src / player / player.gd

=== Attached Scenes
* link:/scenes/src/player/player.tscn[res://src/player/player.tscn]



=== Code
```gdscript
extends CharacterBody2D
class_name Player

@export var GhostEffect : PackedScene

signal dashed
signal failed

const SPEED = 800.0
const DASH_SPEED = SPEED * 4
const JUMP_VELOCITY = -1100.0
const GRAVITY = 4000
const ALLOWED_TIME_TO_JUMP = 0.13
const DASH_TIME = 0.1
const DASH_ATTENUATION = Vector2(55, 50)
const MAX_DASH_COUNT = 1
const DEFAULT_SCALE = Vector2(6.0, 6.0)
var falling_time := 0.0
var dash_rest_time := 0.0
var dash_rest_count := MAX_DASH_COUNT
var grounding : bool = true
var sprite_tween : Tween
static var playable : bool = false


func _ready() -> void:
	playable = true


func _physics_process(delta: float) -> void:
	if dash_rest_time > 0:
		dash_rest_time -= delta
		velocity *= DASH_ATTENUATION * delta
	else:
		# Add the gravity.
		if not is_on_floor():
			# FIXME: ignore gravity if failed
			velocity.y += GRAVITY * delta
			falling_time += delta
			grounding = false
		else:
			if not grounding:
				idle()
				tween_sprite(&'grounding')
				vibrate('Landed')
			grounding = true
			falling_time = 0
			dash_rest_count = MAX_DASH_COUNT
		
		if not playable:
			if velocity:
				velocity.x = move_toward(velocity.x, 0, SPEED)
				if not velocity: idle()
			update_sprite_states_shader_parameters()
			move_and_slide()
			return

		# Handle jump.
		if Input.is_action_just_pressed("move_jump") and falling_time < ALLOWED_TIME_TO_JUMP:
			velocity.y = JUMP_VELOCITY
			tween_sprite(&'jumping')

		var direction_x := Input.get_axis("move_left", "move_right")
		if direction_x:
			velocity.x = direction_x * SPEED
		else:
			velocity.x = move_toward(velocity.x, 0, SPEED)

		# Handle dash.
		if Input.is_action_just_pressed("move_dash") && dash_rest_count > 0:
			var direction_y := Input.get_axis("move_up", "move_down")
			var direction = Vector2(direction_x, direction_y).normalized()
			if direction:
				dashed.emit()
				velocity = direction * DASH_SPEED
				dash_rest_time = DASH_TIME
				dash_rest_count -= 1
	
	animate()
	move_and_slide()


func animate() -> void:
	animate_with(velocity)
	update_sprite_states_shader_parameters()
	if dash_rest_time > 0 && int(dash_rest_time*100)%3 > 0:
		spawn_ghost_effect()


func animate_with(_velocity: Vector2) -> void:
	if _velocity.x:
		for state in ['Idle', 'Run', 'Jump', 'Fall']:
			%AnimationTree.set('parameters/%s/blend_position' % [state], _velocity.x)
	%AnimationTree.get('parameters/playback').travel(get_state_by(_velocity))


func idle() -> void:
	animate_with(Vector2(0, 0))


func walk_right() -> void:
	animate_with(Vector2(1, 0))


func walk_left() -> void:
	animate_with(Vector2(-1, 0))


func turn_right() -> void:
	walk_right(); idle()


func turn_left() -> void:
	walk_left(); idle()


func vibrate(key: String):
	var values = [0, 0, 0]
	match key:
		'Dashed': values = [0.4, 0.6, 0.2]
		'Landed': values = [0.1, 0.3, 0.1]
	Input.start_joy_vibration(0, values[0], values[1], values[2])


func get_state_by(_velocity) -> String:
	if _velocity:
		if _velocity.y < 0: return 'Jump'
		if _velocity.y > 0: return 'Fall'
		else: return 'Run'
	else: return 'Idle'


func tween_sprite(key: StringName) -> void:
	if sprite_tween: sprite_tween.kill()
	sprite_tween = %Sprites.create_tween()
	var coef = Vector2(1, 1)
	match key:
		&'dashed':
			var dir := velocity.normalized()
			coef = dir.abs().clamp(Vector2(0.75, 0.75), Vector2(1.0, 1.0))
		&'grounding': coef = Vector2(1.05, 0.95)
		&'jumping':   coef = Vector2(0.70, 1.30)

	sprite_tween.tween_property(%Sprites, 'scale', coef * DEFAULT_SCALE, 0.10) \
		.set_ease(Tween.EASE_IN_OUT).set_trans(Tween.TRANS_CUBIC)
	sprite_tween.tween_property(%Sprites, 'scale', DEFAULT_SCALE, 0.16) \
		.set_ease(Tween.EASE_OUT).set_trans(Tween.TRANS_SINE)


func update_sprite_states_shader_parameters() -> void:
	for state in %SpriteStates.get_children():
		state.material.set_shader_parameter("dashed", is_dashed())


func spawn_ghost_effect() -> void:
	var ghost = GhostEffect.instantiate()
	ghost.spawn(self)


func current_sprite_state() -> Sprite2D:
	return %SpriteStates.get_children().filter(
		func(c): return c.visible)[0]


func is_dashed() -> bool:
	return true if dash_rest_count < MAX_DASH_COUNT else false


func _on_dashed() -> void:
	vibrate('Dashed')
	tween_sprite(&'dashed')
	%Camera.shake(&'dash')


func _on_failed() -> void:
	playable = false
	%AnimationTree.active = false
	%AnimationPlayer.play("burst")
	%RestartCurtain.play()


func _on_frame_subject_area_entered(area: Area2D) -> void:
	%Camera.add_frame(area)


func _on_frame_subject_area_exited(area: Area2D) -> void:
	%Camera.remove_frame(area)


func _on_failure_collision_area_entered(area: Area2D) -> void:
	failed.emit()

```
